// Usage: ./gradlew runIntegrationTests -Pserver.host=<> -Pserver.port=<> -Pserver.username=<>, -Pserver.password=<>
// ./gradlew build will still compile Java, but not run the test task


ext {
	server_host = project.hasProperty('server.host') ? project.getProperty('server.host') : "undefined"
	server_port = project.hasProperty('server.port') ? project.getProperty('server.port') : -1
	username = project.hasProperty('server.username') ? project.getProperty('server.username') : "undefined"
	password = project.hasProperty('server.password') ? project.getProperty('server.password') : "undefined"
	server_test_directory = project.hasProperty('server.test.directory') ? project.getProperty('server.test.directory') : "undefined"
	test_version = project.hasProperty('test.version') ? project.getProperty('test.version') : "undefined"
}

dependencies {
    api project(':data-sets-model')
    api project(':data-sets-api-server')

    implementation libraries.spring_boot_starter_actuator
    implementation libraries.spring_boot_starter_parent
    implementation libraries.spring_boot_starter_security
	implementation libraries.spring_security_config
	implementation libraries.spring_security_core
	implementation libraries.spring_security_crypto
	implementation libraries.spring_security_web
    implementation libraries.spring_boot_starter_web
	implementation libraries.spring_aop
	implementation libraries.spring_beans
	implementation libraries.spring_context
	implementation libraries.spring_core
	implementation libraries.spring_expression
	implementation libraries.spring_messaging
	implementation libraries.spring_jcl
	implementation libraries.spring_test
	implementation libraries.spring_web
	implementation libraries.spring_webmvc
    implementation libraries.http_core
    implementation libraries.http_client
    implementation libraries.commons_codec
    implementation libraries.gson
	implementation libraries.snakeyaml
	implementation libraries.tomcat_annotations_api
	implementation libraries.tomcat_embed_core
	implementation libraries.tomcat_embed_websocket
    compileOnly libraries.lombok
    implementation libraries.explorer_api_common
	testImplementation libraries.restAssured

    testImplementation libraries.spring_boot_starter_test
    testImplementation libraries.powermock_api_mockito2
    testImplementation libraries.power_mock_junit4
    testImplementation libraries.power_mock_junit4_rule
    testImplementation libraries.mockito_core
    testImplementation libraries.explorer_api_common_test
	testImplementation 'org.hamcrest:hamcrest-junit:2.0.0.0' //TODO testCompile libraries.hamcrest
}

test {
	systemProperty "server.host", server_host
	systemProperty "server.port", server_port
	systemProperty "server.username", username
	systemProperty "server.password", password
	systemProperty "server.test.directory", server_test_directory
	systemProperty "test.version", test_version

	reports.junitXml.destination = file(
		file(reports.junitXml.destination).getParent() + 
		"/test-" + 
		test_version
	)
	reports.html.destination = file(
		file(reports.html.destination).getParent() + 
		"/test-" + 
		test_version
	)
}

task validate {
	doLast {
		checkAndThrow("server_host", "undefined", "server.host");
		checkAndThrow("server_port", -1, "server.port");
		checkAndThrow("username", "undefined", "server.username");
		checkAndThrow("password", "undefined", "server.password");
		checkAndThrow("server_test_directory", "undefined", "server.test.directory");
		checkAndThrow("test_version", "undefined", "test.version");
	}
}	

task runIntegrationTests(dependsOn: ['validate', 'test']){

}

void checkAndThrow(String property, defaultValue, required=property) {
	if (ext.getProperty(property).equals(defaultValue)) {
		throw new GradleException("Missing the required property \'" + required + "\'.");
	}
}

gradle.taskGraph.whenReady { taskGraph -> 
	if (!taskGraph.hasTask(runIntegrationTests)) {
		def tasks = taskGraph.getAllTasks()
		tasks.findAll { it.name == 'test' }.each { task -> 
			if (task.getProject().getName() == 'data-sets-tests') {
		 		task.setEnabled(false) 
			}
		}
	}
} 

