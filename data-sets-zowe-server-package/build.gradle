ext {
    artifactName = 'files-server-zowe-package'
}

configurations {
    dataSetsApiServerJar
    zip
}

dependencies {
    dataSetsApiServerJar(project(path: ":data-sets-api-server", configuration: "bootArchives"))
}

task packageDataSetsApiServer(type: Zip) {
    archiveName = artifactName + ".zip"
    includeEmptyDirs = true
    def resourceDir = 'src/main/resources/'

    into('/') {
        from "$resourceDir/manifest.yaml", "$resourceDir/apiml-static-registration.yaml.template"
    }

    into('bin/') {
        from configurations.dataSetsApiServerJar, "$resourceDir/bin/start.sh", "$resourceDir/bin/configure.sh", "$resourceDir/bin/validate.sh"
    }
}

jar.dependsOn(packageDataSetsApiServer)
build.dependsOn(packageDataSetsApiServer)

artifacts {
    zip packageDataSetsApiServer
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact("$buildDir/distributions/$artifactName" + ".zip")
        }
    }
}
